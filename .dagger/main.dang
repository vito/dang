type Dang {
  """
  The source directory for the Dang project.
  """
  pub source: Directory! @defaultPath(path: "/") @ignorePatterns(patterns: [
    # TODO: respecting .gitignore would be nice
    "Session.vim"
    "dang"
    "zed-dang/grammars/"
    ".env"
    ".envrc"
    ".dagger"
  ])

  """
  Return a compiled Dang binary.
  """
  pub binary: File! {
    base.
      withExec(["go", "build", "-o", "/bin/dang", "./cmd/dang"]).
      file("/bin/dang")
  }

  """
  Build the Dang binary to ./bin/dang.
  """
  pub build: Changeset! {
    source.withFile("bin/dang", binary).changes(source)
  }

  """
  Runs all code generation procedures, e.g. go generate, tree-sitter generate.
  """
  pub generate: Changeset! {
    generated.directory(".").changes(base.directory("/src"))
  }

  """
  Run the Dang tests.
  """
  pub test(filter: String! = ""): Void {
    let args = ["go", "test", "-v", "-count=1"]
    if filter != "" {
      args += ["./tests", "-run", "/"+filter]
    } else {
      args += ["./..."]
    }
    generated.
      withExec(args, experimentalPrivilegedNesting: true).
      sync
    null
  }

  """
  Update the Golden-style test specimens, treating the current output as
  correct.
  """
  pub testUpdate: Changeset! {
    generated.
      withExec(["go", "test", "-v", "./tests", "-update"],
        experimentalPrivilegedNesting: true).
      directory(".").
      changes(generated.directory("."))
  }

  """
  Run the tree-sitter tests.
  """
  pub treesitterTest: Void {
    generated.
      withWorkdir("./treesitter").
      withExec(["tree-sitter", "test"]).
      sync
    null
  }

  """
  Update the tree-sitter test corpus.
  """
  pub treesitterUpdate: Changeset! {
    generated.
      withDirectory("./treesitter",
        generated.
          withWorkdir("./treesitter").
          withExec(["tree-sitter", "test", "--update"]).
          directory(".")).
      directory(".").
      changes(generated.directory("."))
  }

  """
  Run the linter.
  """
  pub lint: Void {
    base.
      withEnvVariable("GOLANGCI_LINT_CACHE", "/var/cache/golangci-lint").
      withMountedCache("/var/cache/golangci-lint", cacheVolume("golangci-lint-cache")).
      withExec(["golangci-lint", "run"]).
      sync
    null
  }

  let base: Container! {
    apko.wolfi([
      "bash",
      "go", "golangci-lint",
      "nodejs", "npm",
      "neovim",
    ]).
      withMountedCache("/go/pkg/mod", cacheVolume("go-mod")).
      withEnvVariable("GOMODCACHE", "/go/pkg/mod").
      withMountedCache("/go/build-cache", cacheVolume("go-build")).
      withEnvVariable("GOCACHE", "/go/build-cache").
      withExec(["npm", "install", "-g", "tree-sitter-cli@v0.25.8"]).
      withDirectory("/src", source).
      withWorkdir("/src")
  }

  let generated: Container! {
    base.withExec(["./hack/generate"])
  }
}
